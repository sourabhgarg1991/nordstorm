import com.github.jk1.license.render.InventoryHtmlReportRenderer

plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.diffplug.spotless' version '7.0.4'
    id 'org.owasp.dependencycheck' version '12.1.1'
    id 'com.github.jk1.dependency-license-report' version '2.9'
}

ext {
    lombokVersion = '1.18.38'
    log4jVersion = '2.24.3'
    junitVersion = '5.13.0'
    mockitoVersion = '5.18.0'
    schemaRepository = '2133'
}

group = 'com.nordstrom.finance.dataintegration'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    configureEach {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
}

dependencies {
    implementation "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation("org.slf4j:slf4j-api:2.0.12")
    implementation ("org.apache.logging.log4j:log4j-api:${log4jVersion}")
    implementation ("org.apache.logging.log4j:log4j-core:${log4jVersion}")
    implementation ("org.apache.logging.log4j:log4j-slf4j2-impl:${log4jVersion}")

    implementation "org.springframework.boot:spring-boot-starter"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    implementation "org.aspectj:aspectjweaver:1.9.19"

    implementation "org.springframework.kafka:spring-kafka"
    implementation "io.confluent:kafka-avro-serializer:7.9.1"
    implementation "org.apache.avro:avro:1.12.0"
    implementation "org.apache.kafka:kafka-clients:3.9.1"

    implementation "com.nordstrom.sig:schema-repository:${schemaRepository}"

    implementation "com.newrelic.agent.java:newrelic-api:8.4.0"
    implementation "com.datadoghq:java-dogstatsd-client:4.4.3"

    implementation "org.slf4j:slf4j-api:2.0.12"
    implementation "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    implementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    implementation "org.apache.logging.log4j:log4j-slf4j2-impl:${log4jVersion}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.7.4"
    implementation "com.lmax:disruptor:3.4.4"
    implementation "com.datadoghq:java-dogstatsd-client:4.4.3"

    implementation "com.nordstrom.finance.dataintegration.common:data-integration-common:1.0.0"

    runtimeOnly("org.postgresql:postgresql:42.7.3")

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.assertj:assertj-core:3.6.1"
    testImplementation "org.springframework.kafka:spring-kafka-test"
}

configurations {
    configureEach {
        // remove Boot's default logging (Logback + starter-logging)
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events = ["standardOut", "started", "passed", "skipped", "failed"]
        showStandardStreams = true
        exceptionFormat = 'full'
    }
    finalizedBy jacocoTestReport
    jacoco {
        destinationFile = layout.buildDirectory.file("jacoco/test.exec").get().asFile
    }
}

spotless {
    java {
        googleJavaFormat('1.17.0')
        removeUnusedImports()
    }
}


jacoco {
    toolVersion = "0.8.13"
    reportsDirectory = layout.buildDirectory.dir("reports/jacoco/test")
}

tasks.register('coverage') {
    dependsOn {
        jacocoTestReport
    }
    doLast {
        println "Test coverage report: " + jacocoTestReport.reports.getHtml().getEntryPoint().toURI()
    }
}

def jacocoExcludeList = [
        '**/config/**',
        '**/exception/**',
        '**/model/**',
        '**/constant/**',
        '**/entity/**',
        '**/repository/**',
        '**/readiness/**',
        'com/nordstrom/finance/dataintegration/Application.class']

jacocoTestReport {
    reports {
        csv.required = true
        xml.required = true
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco/test')
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: jacocoExcludeList)
        }))
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                minimum = 0.8
            }
        }
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: jacocoExcludeList)
        }))
    }
}

licenseReport {
    renderers = new InventoryHtmlReportRenderer('grouped-license-report.html')
}

dependencyCheck {
    failBuildOnCVSS = 1
    suppressionFile='dependency-audit-suppression.xml'
    analyzers {
        assemblyEnabled = false
        artifactory {
            enabled = true
            url = 'https://artifactory.nordstrom.com/artifactory'
            username = artifactoryUsername
            apiToken = artifactoryPassword
            assemblyEnabled = false
        }
    }
}

sourceSets {
    integrationTest {
        java {
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output + compileClasspath
        annotationProcessorPath += main.annotationProcessorPath
        compileClasspath += configurations['testRuntimeClasspath']
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation, implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
    integrationAnnotationProcessor.extendsFrom annotationProcessor
    integrationCompileOnly.extendsFrom compileOnly
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    useJUnitPlatform()
    testLogging {
        events = ["standardOut", "started", "passed", "skipped", "failed"]
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

tasks.register('jacocoIntegrationTestReport', JacocoReport) {
    dependsOn integrationTest
    sourceSets sourceSets.main
    executionData layout.buildDirectory.file("jacoco/integrationTest.exec").get().asFile
    reports {
        xml.required = true
        html.outputLocation.set(layout.buildDirectory.dir('reports/jacoco/integrationTest'))
        csv.required = false
    }
}

apply plugin: 'idea'
idea {
    module {
        testSources.from(file('integrationTest/java'))
    }
}

integrationTest {
    finalizedBy jacocoIntegrationTestReport
    jacoco {
        destinationFile = layout.buildDirectory.file("jacoco/integrationTest.exec").get().asFile
    }
}

check.dependsOn jacocoTestReport
check.dependsOn jacocoTestCoverageVerification